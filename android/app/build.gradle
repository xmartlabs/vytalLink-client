plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def findGoogleServicesJson() {
    return [
            file("google-services.json"),
            file("src/google-services.json"),
            file("src/debug/google-services.json"),
            file("src/release/google-services.json"),
            file("src/dev/google-services.json"),
            file("src/prod/google-services.json"),
            file("src/dev/debug/google-services.json"),
            file("src/dev/release/google-services.json"),
            file("src/prod/debug/google-services.json"),
            file("src/prod/release/google-services.json")
    ].find { it.exists() }
}

if (findGoogleServicesJson()) {
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
} else {
    println "⚠️ google-services.json not found. Skipping Google Services & Crashlytics plugins."
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply from: rootProject.file('read_properties.gradle')

android {

    // ----- BEGIN flavorDimensions (autogenerated by flutter_flavorizr) -----
    flavorDimensions += "flavor-type"

    productFlavors {
        dev {
            dimension "flavor-type"
            applicationId "com.xmartlabs.vytallink.dev"
            resValue "string", "app_name", "VytalLink Dev"
        }
        staging {
            dimension "flavor-type"
            applicationId "com.xmartlabs.vytallink.sta"
            resValue "string", "app_name", "VytalLink Sta"
        }
        prod {
            dimension "flavor-type"
            applicationId "com.xmartlabs.vytallink"
            resValue "string", "app_name", "VytalLink"
        }
    }

    // ----- END flavorDimensions (autogenerated by flutter_flavorizr) -----

    namespace "com.xmartlabs.vytalLink"

    compileSdk 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.xmartlabs.flutter_template"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            storeFile getEnvVariable('KEYSTORE_FILE') == null ? null : file(getEnvVariable('KEYSTORE_FILE'))
            storePassword getEnvVariable('KEYSTORE_PASSWORD')
            keyAlias getEnvVariable('KEYSTORE_KEY_ALIAS')
            keyPassword getEnvVariable('KEYSTORE_KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
}

flutter {
    source '../..'
}
